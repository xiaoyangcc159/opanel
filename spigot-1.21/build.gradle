plugins {
    id 'com.gradleup.shadow' version '8.3.8'
}

configurations {
    shadowImplementation
    implementation.extendsFrom(shadowImplementation)
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc"
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
    maven {
        name = "mojang"
        url = uri("https://libraries.minecraft.net")
    }
}

archivesBaseName = "${rootProject.name}-spigot"

dependencies {
    compileOnly "org.spigotmc:spigot-api:${minecraft_version}-R0.1-SNAPSHOT"
    shadowImplementation "de.tr7zw:item-nbt-api:2.15.2"
    compileOnly "org.apache.logging.log4j:log4j-core:2.24.1"
    compileOnly "com.mojang:brigadier:1.0.18"

    shadowImplementation(project(":core")) {
        // To avoid conflict
        exclude group: 'com.github.oshi', module: 'oshi-core'
    }
    compileOnly(project(":bukkit-helper"))

    implementation "com.github.oshi:oshi-core:6.8.2"
}

shadowJar {
    configurations = [project.configurations.shadowImplementation]

    archiveBaseName = baseName +"-build"
    archiveClassifier = ''
    destinationDirectory = file "../build/libs"

    relocate 'com.google.gson', 'net.opanel.deps.gson'
    relocate 'org.objectweb.asm', 'net.opanel.deps.objectweb.asm'
    relocate 'net.kyori', 'net.opanel.deps.kyori'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.opanel.deps.nbtapi'
    relocate 'io.javalin', 'net.opanel.deps.javalin'
    relocate 'kotlin', 'net.opanel.deps.kotlin'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.named('compileJava', JavaCompile) {
    source(project(":bukkit-helper").sourceSets.main.allSource)
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            version: project.version,
            api_version: api_version
    ]
    inputs.properties replaceProperties

    filesMatching("plugin.yml") {
        expand replaceProperties + [project: project]
    }

    from project(":bukkit-helper").sourceSets.main.resources
}

build {
    dependsOn shadowJar
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
