plugins {
    id 'java-library'
}

base {
    archivesName = "${rootProject.name}-core"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.11.0'
    api 'com.github.oshi:oshi-core:6.8.2'
    implementation 'io.javalin:javalin:5.6.4'
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.13.0'
    implementation 'com.cronutils:cron-utils:9.2.1'
}

tasks.register('generateOPanelProperties') {
    def resourcesDir = sourceSets.main.output.resourcesDir
    def propertiesFile = new File(resourcesDir, "opanel.properties")
    outputs.file(propertiesFile)

    doLast {
        resourcesDir.mkdirs()

        propertiesFile.createNewFile()
        Properties props = new Properties()
        props.setProperty("version", project.version.toString())
        propertiesFile.withWriter('UTF-8') {writer ->
            props.store(writer, "Generated by Gradle")
        }
    }
}

processResources {
    dependsOn generateOPanelProperties
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(14)
    }
}
