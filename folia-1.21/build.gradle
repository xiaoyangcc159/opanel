plugins {
    id 'com.gradleup.shadow' version '8.3.8'
}

configurations {
    shadowImplementation
    implementation.extendsFrom(shadowImplementation)
}

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
}

base.archivesName = "${rootProject.name}-folia"

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    shadowImplementation "de.tr7zw:item-nbt-api:2.15.2"
    compileOnly "org.apache.logging.log4j:log4j-core:2.24.1"

    shadowImplementation(project(":core")) {
        // To avoid conflict
        exclude group: 'com.github.oshi', module: 'oshi-core'
    }

    implementation "com.github.oshi:oshi-core:6.8.2"
}

shadowJar {
    configurations = [project.configurations.shadowImplementation]

    archiveBaseName = baseName +"-build"
    archiveClassifier = ''
    destinationDirectory = file "../build/libs"

    relocate 'com.google.gson', 'net.opanel.deps.gson'
    relocate 'org.objectweb.asm', 'net.opanel.deps.objectweb.asm'
    relocate 'net.kyori', 'net.opanel.deps.kyori'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.opanel.deps.nbtapi'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            version: project.version,
            minecraft_version: minecraft_version
    ]
    inputs.properties replaceProperties

    filesMatching("plugin.yml") {
        expand replaceProperties + [project: project]
    }
}

build {
    dependsOn shadowJar
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaLanguageVersion.of(21)